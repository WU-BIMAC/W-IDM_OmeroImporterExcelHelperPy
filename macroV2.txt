Sub PyOmeroImporterExcelHelper()
    Dim isOverride As Boolean
    Dim isDebug As Boolean
    Dim cellVal As Variant
    Dim osType As String
    Dim scriptPath As String
    Dim command As String
    Dim excelPath As String
    Dim result As String
    
    On Error GoTo ErrorHandler
    
    cellVal = Range("C9").Value
    isDebug = False
    If cellVal = True Then
        isDebug = True
    End If
    
    Dim ws As Worksheet
    Dim lastRow As Long
    
    cellVal = Range("B9").Value
    isOverride = False
    If cellVal = True Then
        isOverride = True
    End If
    If Not isOverride Then
        Response = MsgBox("The macro you are trying to run is going" & vbCrLf & "to override all the entries in the table below." & vbCrLf & "Are you sure you want to continue?", vbQuestion + vbYesNo, "Confirmation")

        ' Check the user's response
        If Response = vbNo Then
            ' Code to execute if the user clicks "Yes"
            Exit Sub
        End If
    End If
    
    ' Use the active sheet
    Set ws = ActiveSheet
    ' Check if column A is entirely filled
    If ws.Cells(ws.Rows.Count, 1).Value <> "" Then
        ' If the last cell in column A is not empty, assume all rows are filled
        lastRow = ws.Rows.Count
    Else
        ' Otherwise, find the last row with data in column A
        lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
    End If
    ' Debugging: Show the last row to delete
    'MsgBox "Last row to delete: " & lastRow
    ' Check if there are any rows to delete
    If lastRow >= 14 Then
        ' Clear the content of rows from row 14 to the last row
        ws.Rows("14:" & lastRow).ClearContents
    ' Else
    '    MsgBox "No rows to delete starting from row 14.", vbInformation
    End If
    
    ' Get the OS
    osType = Application.OperatingSystem
    ' Get the path of the Excel workbook this is being run from
    excelPath = Chr(34) & Application.ThisWorkbook.FullName & Chr(34)
    
    If InStr(osType, "Macintosh") > 0 Then
        ' Set the path to the Shell script
        If isDebug Then
            ' Find the script in the current directory
            scriptPath = Chr(34) & Application.ThisWorkbook.Path & "/PyOmeroImporterExcelHelper.sh" & Chr(34)
        Else
            ' Find the script in the home directory under OmeroImporter
            scriptPath = Chr(34) & "~/OmeroImporter/PyOmeroImporterExcelHelper.sh" & Chr(34)
        End If
        ' Launch the shell script on Shell
        command = scriptPath & Chr(32) & excelPath & Chr(32) & isDebug
        #If Mac Then
            MsgBox "Macro completed, cells should be filling shortly"
            result = AppleScriptTask("PyOmeroImporterExcelHelper.scpt", "ExecuteScript", command)
        #End If
    ElseIf InStr(osType, "Windows") > 0 Then
        ' Set the path to the PowerShell script
        If isDebug Then
            ' Find the script in the current directory
            scriptPath = Chr(34) & Application.ThisWorkbook.Path & "\PyOmeroImporterExcelHelper.ps1" & Chr(34)
        Else
            ' Find the script in the home directory under OmeroImporter, need to find home directory first
            homeDirectory = VBA.Environ$("USERPROFILE")
            If Right(homeDirectory, 1) <> "\" Then
                ' Add backslash if not already present
                homeDirectory = homeDirectory & "\"
            End If
            'Build path from home directory
            scriptPath = Chr(34) & homeDirectory & "OmeroImporter\PyOmeroImporterExcelHelper.ps1" & Chr(34)
        End If
        ' Launch the PowerShell script on Windows
        command = "powershell -NoExit -ExecutionPolicy Bypass -File" & Chr(32) & scriptPath & Chr(32) & excelPath & Chr(32) & isDebug
        MsgBox "Macro completed, cells should be filling shortly"
        If isDebug Then
            Call Shell(command, vbNormalFocus)
        Else
            Call Shell(command, vbHide)
        End If
    Else
        MsgBox "Unsupported OS"
    End If
CleanExit:
    'cleanup code here
    Exit Sub
ErrorHandler:
    MsgBox err.Description
    Resume CleanExit
End Sub
